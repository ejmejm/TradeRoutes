plugins {
    id 'java'
    id("io.papermc.paperweight.userdev") version "1.7.2"
    id("xyz.jpenilla.run-paper") version "2.3.0"
//    id("xyz.jpenilla.resource-factory-bukkit-convention") version "1.1.1"
    id("io.github.goooler.shadow") version "8.1.7"
}

group = 'io.github.ejmejm'
version = '1.0-SNAPSHOT'

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

// paperweight.reobfArtifactConfiguration = io.papermc.paperweight.userdev.ReobfArtifactConfiguration.MOJANG_PRODUCTION

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "fancynpcs"
        url = "https://repo.fancyplugins.de/releases"
    }
    maven {
        name = 'glaremasters repo'
        url = 'https://repo.glaremasters.me/repository/towny/'
    }
}

dependencies {
    paperweight.paperDevBundle("1.21-R0.1-SNAPSHOT")
//    compileOnly("io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT")
    compileOnly("de.oliver:FancyNpcs:2.2.2")
    implementation("org.xerial:sqlite-jdbc:3.46.0.1")
    implementation("com.j256.ormlite:ormlite-core:6.1")
    implementation("com.j256.ormlite:ormlite-jdbc:6.1")
    compileOnly('com.palmergames.bukkit.towny:towny:0.100.3.12')
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.jar {
    manifest {
        attributes["paperweight-mappings-namespace"] = "mojang"
    }
}

tasks {
    compileJava {
        // Set the release flag. This configures what version bytecode the compiler will emit, as well as what JDK APIs are usable.
        // See https://openjdk.java.net/jeps/247 for more information.
        options.release = 21
    }
    shadowJar {
        relocate("com.j256.ormlite", "io.github.ejmejm.tradeRoutes.dependency.ormlite")
    }
    runServer {
        downloadPlugins {
            hangar("ViaVersion", "5.0.3")
            // Towny
            url("https://drive.usercontent.google.com/u/0/uc?id=1RS7dknkoxGDJdermOn4_dIQye4dC7syg&export=download")
            // Towny Chat
            url("https://drive.usercontent.google.com/u/0/uc?id=1o7l8QKF9JwC8zxZvmDmkW_VyDEbD7Bw2&export=download")
            // FancyNpcs
            modrinth("fancynpcs", "2.2.2")
        }

        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21.1")
        jvmArgs("-Dcom.mojang.eula.agree=true")
    }
}